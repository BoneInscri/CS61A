## 1.3 Formulating Abstractions with Higher-Order Procedures



（1）任何一门语言都应该具有定义过程的能力

（2）过程是一种抽象

比如：

```lisp
(define (cube x) (* x x x))
```

上面的过程就是 下面三个表达式的抽象

```lisp
(* 3 3 3)
(* x x x)
(* y y y) 
```



过程也可以被操作，**操作过程的过程被称为高阶过程**

Procedures that manipulate procedures are called **higher-order procedures.**

我们**需要构造可以接受过程作为参数或将过程作为值返回的过程**。

即我们不能限制过程的参数只能是数字，还可以让其参数设置为过程！

### 1.3.1 Procedures as Arguments

```lisp
;  the sum of the integers from a through b
(define (sum-integers a b)
  (if (> a b)
      0
      (+ a (sum-integers (+ a 1) b))))

; the sum of the cubes of the integers in the given range
(define (sum-cubes a b)
  (if (> a b)
      0
      (+ (cube a) (sum-cubes (+ a 1) b))))
```

还可以计算特定的序列？

```lisp
(define (pi-sum a b)
  (if (> a b)
      0
      (+ (/ 1.0 (* a (+ a 2))) (pi-sum (+ a 4) b))))
```

$$
\left(\pi/4\right)=1-\left(1/3\right)+\left(1/5\right)-\left(1/7\right)+\cdots,\\
\pi/8=\frac{1}{1\cdot3}+\frac{1}{5\cdot7}+\frac{1}{9\cdot11}+\cdots
$$

在很大程度上是相同的，**不同之处在于**

- 过程的**名称**
- 用于计算要添加的**项的a的函数**
- 提供**a的下一个值的函数**

主要就两点：**每个项长什么样？如何得到下一个项？**

```lisp
(define (<name> a b)
  (if (> a b)
      0
      (+ (<term> a)
         (<name> (<next> a) b))))
```

这其实就是下面的求和过程，即Sima符号就是这么来的：
$$
\sum_{n=a}^{b}f(n)=f(a)+\cdots+f(b)
$$
于是sum 可以写成：

```lisp
(define (sum term a next b)
  (if (> a b)
      0
      (+ (term a)
         (sum term (next a) next b))))
```

立方和可以写成下面的形式：

```lisp
(define (sum-cubes a b)
	(define (inc n) (+ n 1))
	(define (cube a) (* a a a))
    (sum cube a inc b))
(sum-cubes 1 10)
; 3025
```

直接求一个范围的和，可以写成下面的形式：

```lisp
(define (sum-integers a b)
    (define (inc n) (+ n 1))
    (define (identity x) x)
    (sum identity a inc b))
(sum-integers 1 10)
; 55
```

pi 也可以通过下面的方式进行计算：

```lisp
(define (pi-sum a b)
  (define (pi-term x)
    (/ 1.0 (* x (+ x 2))))
  (define (pi-next x)
    (+ x 4))
  (sum pi-term a pi-next b))
(* 8 (pi-sum 1 1000))
; 3.139592655589783
```

通过上面的高阶过程的抽象，可以实现a 到 b 的定积分：

```lisp
(define (integral f a b dx)
  (define (add-dx x) (+ x dx))
  (* (sum f (+ a (/ dx 2.0)) add-dx b) dx))
(integral cube 0 1 0.01)
.24998750000000042
(integral cube 0 1 0.001)
.249999875000001
```

$$
\int_{a}^{b}f=\left[f\left(a+\frac{dx}{2}\right)+f\left(a+dx+\frac{dx}{2}\right)+f\left(a+2dx+\frac{dx}{2}\right)+\cdots\right]dx
$$

(The exact value of **the integral of `cube` between 0 and 1 is 1/4.**)

###  1.3.2 Constructing Procedures Using `Lambda`

引入特殊形式lambda来实现锁碎的过程。

来代替 pi-next、pi-term 等。

```lisp
(define (pi-next x)
    (+ x 4))
(lambda (x) (+ x 4))
(define (pi-term x)
    (/ 1.0 (* x (+ x 2))))
(lambda (x) (/ 1.0 (* x (+ x 2))))
```

从而不需要定义辅助过程！

```lisp
(define (pi-sum a b)
    (sum (lambda (x) (/ 1.0 (* x (+ x 2))))
         a
         (lambda (x) (+ x 4))
         b))
```

修改的积分过程入下：

```lisp
(define (integral f a b dx)
  (* (sum f
          (+ a (/ dx 2.0))
          (lambda (x) (+ x dx))
          b)
     dx))
```

下面是lambda表达式的形式：

```lisp
(lambda (<formal-parameters>) <body>)
```

没有名称

The only difference is that **it has not been associated with any name in the environment.**

```lisp
(define (plus4 x) (+ x 4))
(define plus4 (lambda (x) (+ x 4)))
(lambda (x) (+ x 4))
; the procedure   of an argument x  that adds  x and 4
```

上面的两个表达式等价！

```lisp
((lambda (x y z) (+ x y (square z))) 1 2 3)
; 12
```

可以组合！

lambda符号源于微积分，数学逻辑学家**阿朗佐·丘奇(1941)**引入的数学形式主义

实际上 lambda 的含义就是 make-procedure，即一个**构造过程的过程**，即返回类型为过程的过程。



**即过程可以作为过程的参数，过程可以作为过程的值。**



#### Using `let` to create local variables

$$
f(x,y)=r(1+xy)^{2}+y(1-y)+(1+xy)(1-y)
$$

可以引入一些中间变量，也就是局部参数来让过程有更好的可读性：
$$
\begin{array}{rcl}{a}&{=}&{1+xy}\\{b}&{=}&{1-y}\\{f(x,y)}&{=}&{xa^{2}+yb+ab}\\\end{array}
$$
过程描述如下：

```lisp
(define (f x y)
  (define (f-helper a b)
    (+ (* x (square a))
       (* y b)
       (* a b)))
  (f-helper (+ 1 (* x y)) 
            (- 1 y)))
```

上面是定义一个有名的过程，然后显示调用这个过程。

还可以定义一个匿名的lambda表达式，然后调用它**（还需要加一个括号！）**。

```lisp
(define (f x y)
    ((lambda (a b)
             (+ (* x (square a))
                (* y b)
                (* a b)))
     (+ 1 (* x y))
     (- 1 y)))
```

如果引入了let来定义局部变量，那么过程描述入下：

```lisp
(define (f x y)
    (let ((a (+ 1 (* x y)))
          (b (- 1 y)))
         (+ (* x (square a))
            (* y b)
            (* a b))))
```

let的一般表达式入下：

```lisp
(let ((<var1> <exp1>)
      (<var2> <exp2>)
		...      
      (<varn> <expn>))
   <body>)
```

```
let	
<var1> have the value <exp1> and
<var2> have the value <exp2> and
...
<varn> have the value <expn>
in <body>
```

**就是在body这个作用区域内定义了n个局部变量！**

let 还可以解释成下面的形式，即加一层，将局部变量改成lambda+参数的形式，那么参数就是let 定义的变量。（太妙了）

```lisp
((lambda (<var1> ...<varn>)
    <body>)
 <exp1>
 ...
 <expn>)
```

let表达式只是底层lambda应用程序的**语法糖。**



let的好处？

（1）可以让某个变量十分方便地绑定到某个特定的局部作用域

```lisp
(define x 5)
(+ (let ((x 3))
     (+ x (* x 10)))
   x)
; 38
```

（2）let 中找不的变量才去let外找

```lisp
(let ((x 3)
      (y (+ x 2)))
  (* x y))
```



不使用let，直接在内部进行define也可以实现相同的效果！

但是使用let更好，这强迫我们思考那些变量属于当前的作用域。

```lisp
(define (f x y)
  (define a (+ 1 (* x y)))
  (define b (- 1 y))
  (+ (* x (square a))
     (* y b)
     (* a b)))
```



### **1.3.3 Procedures as General Methods**

- 查找函数的零点
- 不动点的一般方法 

两者都是基于反复改进猜测直到结果满足某些标准的想法。

作为例子来介绍通过过程的抽象，我们可以解决大部分问题。



#### Finding roots of equations by the half-interval method

用半区间法求方程的根

The $half$-interval method is a simple but powerful technique for finding roots of an equation $f(x)=0$, where $f$ is a continuous function. 

查找连续函数的零点

The idea is that, if we are given points $a$ and $b$ such that $f(a)<0<f(b)$, then $f$ must have at least one zero between $a$ and $b$. 

一个大于0，一个小于0，那么这个区间内至少有一个零点

To locate a zero, let $x$ be the average of $a$ and $b$ and compute $f(x)$ 

- If $f(x)>0$, then $f$ must have a zero between $a$ and x. 
- If $f(x)<0$, then $f$ must have a zero between $x$ and $b$. 

计算中点，然后不断修正区间的断点，从而让区间越来越小。

Continuing in this way, we can identify **smaller and smaller intervals** on which $f$ must have a zero. 

**When we reach a point where the interval is small enough, the process stops.** 

区间足够小的时候就停止

Since the interval of uncertainty is reduced by half at each step of the process, the number of steps required grows as $\Theta(\log(L/T))$, where $L$ is the length of the original interval and $T$ is the error tolerance (that is, **the size of the interval we will consider  "small enough"**). 

Here is a procedure that implements this strategy:

```lisp
(define (close-enough? x y)
  (< (abs (- x y)) 0.001))
(define (search f neg-point pos-point)
  (let ((midpoint (average neg-point pos-point)))
    (if (close-enough? neg-point pos-point)
        midpoint
        (let ((test-value (f midpoint)))
          (cond ((positive? test-value)
                 (search f neg-point midpoint))
                ((negative? test-value)
                 (search f midpoint pos-point))
                (else midpoint))))))
```

和二分查找法十分类似。

good-enough? 和 close-enough? 十分类似。

但是上面的过程描述不完全正确，需要保证 左侧的值小于0，右侧的值大于0。

加一层：

```lisp
(define (half-interval-method f a b)
  (let ((a-value (f a))
        (b-value (f b)))
    (cond ((and (negative? a-value) (positive? b-value))
           (search f a b))
          ((and (negative? b-value) (positive? a-value))
           (search f b a))
          (else
           (error "Values are not of opposite sign" a b)))))
(half-interval-method sin 2.0 4.0)
; 求 sin x=0 在 [2,4] 之间的根
; 3.14111328125
(half-interval-method (lambda (x) (- (* x x x) (* 2 x) 3))
                      1.0
                      2.0)
; 求 x^3-2x-3=0 在 [1,2] 之间的根
; 1.89306640625
```



#### Finding fixed points of functions

寻找函数的不动点

如果x满足方程f(x) = x，则称为函数f的不动点。

对于某些函数f，我们可以通过初始猜测和反复应用f来定位不动点，直到值变化不大为止。
$$
f(x),f(f(x)),f(f(f(x))),\ldots 
$$

```lisp
(define tolerance 0.00001)
(define (fixed-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance))
  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
          next
          (try next))))
  (try first-guess))
(fixed-point cos 1.0)
; .7390822985224023
; 得到 cosx 的不动点
(fixed-point (lambda (y) (+ (sin y) (cos y)))
             1.0)
; 1.2587315962971173
; 得到 y = sin y + cos y的解
```

将计算器设置为弧度模式，然后反复按cos键，直到你得到不动点。



一个数学符号，映射：
$$
y\mapsto x/y
$$
就和lambda表达式 

```lisp
(lambda(y) (/ x y))
```

含义相同



寻找平方根的算法可以等价为寻找不动点

```lisp
(define (sqrt x)
  (fixed-point (lambda (y) (/ x y))
               1.0))
```

但是，并不收敛。。。一直是振荡的。



可以稍微变型一下：
$$
y\mapsto(1/2)(y+x/y)
$$

```
(define (sqrt x)
  (fixed-point (lambda (y) (average y (/ x y)))
               1.0))
```

 *y* = (1/2)(*y* + *x*/*y*) is a simple transformation of the equation *y* = *x*/*y*

to derive it, **add *y* to both sides of the equation and divide by 2**



这种对一个解的**逐次逼近求平均值**的方法，我们称之为**平均阻尼**的技术，**通常有助于不动点搜索的收敛**



### 1.3.4 Procedures as Returned Values

过程可以作为参数传递给过程，过程也可以返回一个过程。

通过创建返回值本身就是过程的过程，我们可以获得更强的表达能力。



平均阻尼：

```lisp
(define (average-damp f)
  (lambda (x) (average x (f x))))
((average-damp square) 10)
; 55 -> ( 10 + 100 ) / 2
```

返回的是一个lambda表达式，即一个过程。

使用上面这个过程，我们可以定义平方根过程：

```lisp
(define (sqrt x)
  (fixed-point (average-damp (lambda (y) (/ x y)))
               1.0))
```

（1）平方根

$y\mapsto x/y$

$y^2 =x$

（2）立方根

$y\mapsto x/y^2$

$y^3=x$

使用不动点来求解立方根

```lisp
(define (cube-root x)
  (fixed-point (average-damp (lambda (y) (/ x (square y))))
               1.0))
```



#### Newton's method

半区间法求解函数的零点问题只是牛顿法的一个特例。

If $x\mapsto g(x)$ is a **differentiable** function, then a solution of the equation $g(x)=0$ is a fixed point of the function $x\mapsto f(x)$ where
$$
f(x)=x-\frac{g(x)}{Dg(x)}
$$
 and **$Dg(x)$ is the derivative of $g$ evaluated at $x.$** 

Newton's method is the use of the fixed-point method we saw above to approximate a solution of the equation **by finding a fixed point of the function f** .

先求原函数g(x) 的导数，然后构造函数f(x)，函数f的不动点就是原函数的零点

For many functions $g$ and for **sufficiently good initial guesses for $x$,** Newton's method converges very rapidly to a solution of $g(x)=0.$

In order to implement Newton's method as a procedure, we must first express the idea of derivative. 

Note that "derivative," like average damping, is something that transforms a function into another function. 

请注意，“导数”和平均阻尼一样，是将一个函数转换为另一个函数的东西。

即泛函，转化函数的“函数”

**For instance, the derivative of the function $x\mapsto x^3$ is the function $x\mapsto3x^2.$** 

In general, if $g$ is a function and $dx$ is a small number, then the derivative $Dg$ of $g$ is the function whose value at any number $x$ is given **(in the limit of small $dx)$** by
$$
Dg(x)=\frac{g(x+dx)-g(x)}{dx}
$$
程序计算支持浮点，那么将dx设置为一个很小的数字就可以了

```lisp
(define (deriv g)
    (define dx 0.00001)
    (lambda (x)
            (/ (- (g (+ x dx)) (g x))
               dx)))
; 计算y = x^3 在 5 处的导数
(define (cube x) (* x x x))
((deriv cube) 5)
; 75.00014999664018
```

使用导数 + 不动点的过程就可以构造牛顿法了：

```lisp
(define (newton-transform g)
  (lambda (x)
    (- x (/ (g x) ((deriv g) x)))))
(define (newtons-method g guess)
  (fixed-point (newton-transform g) guess))
```

对于求平方根，牛顿法从任何起点都能迅速收敛到正确的解。

下面是求解 $y\mapsto y^2-x$ 的零点：

```lisp
(define (sqrt x)
  (newtons-method (lambda (y) (- (square y) x))
                  1.0))
```



#### Abstractions and first-class procedures

更一般的不动点计算方法：

```lisp
(define (fixed-point-of-transform g transform guess)
  (fixed-point (transform g) guess))
```

**使用平均阻尼**+更一般的不动点计算方法实现**平方根**：

```lisp
(define (sqrt x)
  (fixed-point-of-transform (lambda (y) (/ x y))
                            average-damp
                            1.0))
```

**使用牛顿法**+更一般的不动点计算方法实现**平方根**：

```lisp
(define (sqrt x)
  (fixed-point-of-transform (lambda (y) (- (square y) x))
                            newton-transform
                            1.0))
```

实现更加通用的过程需要实现**高阶过程的支持。**

**高阶过程**的意义在于，它们使我们能够将这些**抽象显式地表示为编程语言中的元素**，这样就可以像处理其他计算元素一样处理它们。



第一类的数据满足：

first-class status of programming-language elements

- 它们可以由**变量命名**。
- 它们可以作为**参数传递给过程**。
- 它们可以作为**程序的结果返回**。
- 它们可以包含在**数据结构中**。

编程语言元素的一流地位的概念是由

英国**计算机科学家克里斯托弗·斯特雷奇(Christopher Strachey, 1916-1975)**提出的。

Lisp 的表达能力巨大，但是高效有待提高。







